(ns prolly-tree.adl-test
  (:refer-clojure :exclude [get-in])
  (:require #?(:clj [clojure.test :refer [deftest is testing]]
               :cljs [cljs.test :refer [deftest is testing] :include-macros true])
            [ipld.data-model :as dm]
            [ipld.schema :as ipld-schema]
            [ipld.selector :as selector]
            [prolly-tree.adl :as adl]
            [prolly-tree.core :as tree]
            [prolly-tree.schema :as schema]))

(defn fixture []
  (let [store (atom {})
        put! (fn [cid bytes] (swap! store assoc cid bytes))
        root (tree/build-tree put! [["a" 1] ["b" nil] ["c" {"nested" true}]])]
    {:root root :get-fn #(get @store %) :store store}))

(deftest prolly-tree-is-a-universal-ipld-map-view
  (let [{:keys [root get-fn]} (fixture)
        view (adl/view get-fn root)]
    (is (= :map (dm/kind view)))
    (is (= 1 (dm/lookup view "a")))
    (is (dm/contains-segment? view "b") "present Null is not absence")
    (is (= true (dm/get-in view ["c" "nested"])))
    (is (= [["a" 1] ["b" nil] ["c" {"nested" true}]]
           (vec (dm/entries view))))
    (is (= 3 (dm/length view)))
    (is (= {:root-cid root} (adl/substrate view)))))

(deftest selector-and-schema-see-the-synthesized-map-not-the-tree-layout
  (let [{:keys [root get-fn]} (fixture)
        view (adl/view get-fn root)
        map-schema {:type :map
                    :keys {:type :kind :kind :string}
                    :values {:type :any}}]
    (is (:valid? (ipld-schema/valid? map-schema view)))
    (is (= [{:path ["c" "nested"] :value true}]
           (selector/select view
                            {:selector :explore-fields
                             :fields {"c" {:selector :explore-fields
                                            :fields {"nested" {:selector :matcher}}}}})))))

(deftest malformed-substrate-block-is-rejected-by-schema
  (is (= ["children" 0 1]
         (:path (ipld-schema/valid?
                 schema/node-schema
                 {"kind" "internal" "children" [["z" "plain-cid"]]}))))
  (testing "unknown discriminant"
    (is (false? (:valid? (ipld-schema/valid? schema/node-schema
                                             {"kind" "mystery"}))))))
